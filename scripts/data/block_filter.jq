def txin_coinbase:
    "TxIn {
        script: @from_hex(\"\(.coinbase)\"),
        sequence: \(.sequence),
        previous_output: OutPoint {
            txid: 0x\(0)_u256.into(),
            vout: 0xffffffff_u32,
            data: Default::default(),
            block_height: Default::default(),
            block_time: Default::default(),
        },
        witness: array![].span()
    }"
;

def txin_regular:
    "TxIn {
        script: @from_hex(\"\(.scriptSig.hex)\"),
        sequence: \(.sequence),
        previous_output: OutPoint {
            txid: 0x\(.txid)_u256.into(),
            vout: \(.vout),
            data: Default::default(),
            block_height: Default::default(),
            block_time: Default::default(),
        },
        witness: array![].span()
    }"
;

def txin:
    if .coinbase then
        txin_coinbase
    else
        txin_regular
    end
;

def txout:
    "TxOut {
        value: \((.value*100000000) | round)_u64,
        pk_script: @from_hex(\"\(.scriptPubKey.hex)\"),
        cached: false,
    }"
;

def tx:
    "Transaction {
        version: \(.version),
        is_segwit: \(if (.vin | map(select(has("txinwitness"))) | length) > 0 then true else false end),
        inputs: array![\(.vin | map(txin) | join(",\n"))].span(),
        outputs: array![\(.vout | map(txout) | join(",\n"))].span(),
        lock_time: \(.locktime)
    }"
;

def block:
    "Block {
        header : Header {	
            version: \(.version)_u32,
            time: \(.time)_u32,
            bits: 0x\(.bits)_u32,
            nonce: \(.nonce)_u32
        },
		txs: array![\(.tx | map(tx) | join(",\n"))].span()
   }"
;

def fixture:
"
// THIS CODE IS GENERATED BY SCRIPT, DO NOT EDIT IT MANUALLY

use raito::state::{Block, Header, Transaction, OutPoint, TxIn, TxOut};
use raito::test_utils::from_hex;

pub fn block_\(.height)() -> Block {
    // block hash: \(.hash)
     \( . | block )
}"
;

.result | fixture
